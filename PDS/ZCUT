 /***REXX**************************************************/
 /*   ISPF/PDF EDIT MACRO TO WRITE LINES FROM A FILE TO A */
 /*   ISPF TABLE IN MEMBER FOR LATTER INCLUSION BY THE    */
 /*   PASTE MACRO.                                        */
 /*                                                       */
 /*   FOR HELP ON RUNNING THIS MACRO, UNDER EDIT ENTER:   */
 /*       CUT HELP                                        */
 /*                                                       */
 /*   panel variables:                                    */
 /*                                                       */
 /*       CUTCOUNT        CUT LIMIT FROM PANEL            */
 /*       CUTTBL          TWO CHAR TABLE NAME EXTENSION   */
 /*       ACTCMD          add/replace                     */
 /*                                                       */
 /*                                                       */
 /*********************************************************/
   /* trace i */

   address ispexec "ISREDIT MACRO (P1 P2 P3) NOPROCESS"
   if RC ¬= 0 then
   do
      say 'Error message ' ZERRSM ZERRLM
      exit
   end

   upper P1 P2 P3

   Param.1 = P1
   Param.2 = P2
   Param.3 = P3

 address ispexec
   "CONTROL ERRORS RETURN"
   "VGET (CUTDEF PASTEDEF CUTLIMIT CUTTNDEF) PROFILE"

 ACTCMD = ''

 do i = 1 to 3

    if Param.i = '' then leave

    if (datatype(Param.i,'M') & (length(Param.i) = 2)) then
       CUTTBL = Param.i
    else if datatype(Param.i,'N') then CUTCOUNT = Param.i
         else ACTCMD = Param.i
 end

 "CONTROL NONDISPL"

 do forever

     "DISPLAY PANEL(CUTPROM)"
     if RC = 8 then exit(0)

          /* parse the ACTCMD commands */
    if ACTCMD = 'HELP' then
    do
      "CONTROL DISPL"
      "DISPLAY PANEL(CUTHELP)"
      ACTCMD = ''
      ZEDSMSG = 'ZCUT Help displayed'
      ZEDLMSG = 'Enter ZCUT command.'
      "SETMSG MSG(ISRZ001)"
      exit
    end

    if ACTCMD = 'PROMPT' then
    do
      ACTCMD = ''
      iterate
    end

    /* GET DEFAULT MODE */
    if substr(ACTCMD, 1,1) = 'D' then leave

    /* done?            */
    if substr(ACTCMD, 1,1) = 'A' then leave
    if substr(ACTCMD, 1,1) = 'R' then leave
    if ACTCMD  = '' then leave

 end

 if ACTCMD ¬= 'D' then "CONTROL NONDISPL"

 "DISPLAY PANEL(CUTDEF)"
 if RC = 8 then exit(0)

 TblName = "CUTPST"||CUTTBL

 "TBQUERY CUTTBLL ROWNUM(LISTMAX)"
 if RC ¬= 0 then
 do
    "TBCREATE CUTTBLL KEYS(CUTTBL) NOWRITE SHARE"
    if RC ¬= 0 then do
        "TBOPEN CUTTBLL NOWRITE SHARE"
        if RC ¬= 0 then exit(12)
    end
 end
 "TBADD CUTTBLL"

 /* PROCESS LINE COMMANDS, CHECK if C OR M WAS SPECIFIED */
 "ISREDIT PROCESS RANGE C M"
 if RC  >= 16 then exit(0)
 else if RC >= 4 then
      do
         ZEDSMSG = 'ENTER "C" | "M" LINE CMD'
         ZEDLMSG = 'CUT REQUIRES A "C" OR "M" LINE COMMAND'
         "SETMSG MSG(ISRZ001)"
         exit(12)
      end

 "ISREDIT (CMD) = RANGE_CMD"              /* GET THE COMMAND     */
 "ISREDIT (LINE1) = LINENUM .ZFRANGE"     /* FIRST LINE IN RANGE */
 "ISREDIT (LINE2) = LINENUM .ZLRANGE"     /* LAST LINE IN RANGE  */
 NumToCut = LINE2 - LINE1 + 1

 /* GET THE NUM OF LINES THAT HAVE BEEN CUT BUT NOT PASTED,  */
 /* INITIALIZE TO ZERO if FIRST TIME.                        */

 "TBQUERY "TblName" ROWNUM(PREVMAX)"
 if RC ¬= 0 then
 do
    "TBCREATE "TblName" NAMES(CTPT) NOWRITE SHARE"
    if RC ¬= 0 then do
        "TBOPEN "TblName" NOWRITE SHARE"
        if RC ¬= 0 then exit(12)
    end
    PREVMAX = 0
 end

 "TBBOTTOM "TblName
 if ACTCMD = 'REPLACE' then
 do
    LastCC = 0
    do while (LastCC = 0)
        "TBDELETE "TblName
        LastCC = RC
    end
    PREVMAX  = 0
 end
 PrevMaxSave = PREVMAX + 0
 PREVMAX = PREVMAX + 0

 /* CHECK TO SEE if THE LIMIT WILL BE EXCEEDED BY STORING   */
 /* THE LINES                                               */
 TheCount = PREVMAX + NumToCut
 if TheCount > CUTCOUNT then
 do
    ZEDSMSG = "> " || CUTCOUNT || ' lines to cut'
    ZEDLMSG = 'REDUCE THE RANGE OR CHANGE LIMIT WITH "CUT 'TheCount'"'
    "SETMSG MSG(ISRZ001)"
    exit(12)
 end

 /* CUT THE LINES TO THE TABLE */
 CutCnt = PREVMAX
 CTPT   = ''
 do i = LINE1 to LINE2
   CutCnt = CutCnt + 1
   "ISREDIT (CTPT) = LINE "i
   "TBADD "TblName
 END

 /* If the number of lines cut this time   */
 /* is greater than the maximum cut,       */
 /* then save the new maximum.             */
 /* Paste will set this variable to        */
 /* zero if it deletes the variables.      */

 if CMD = 'M' then
 do
    "ISREDIT DELETE " LINE1 LINE2
    if PrevMaxSave = 0 then
    do
       ZEDSMSG = NumToCut || " lines cut and deleted"
       ZEDLMSG = NumToCut || " lines were cut and deleted ",
                                "from the current file"
    end
    else do
       ZEDSMSG = NumToCut || " lines cut and deleted"
       ZEDLMSG = "A total of " || PrevMax || " lines have been cut"
                   "the last " || NumToCut || " were deleted"
    end
    "SETMSG MSG(ISRZ000)"
 end
 else do
   if PrevMaxSave = 0 then
   do
      ZEDSMSG = NumToCut || " lines cut"
      ZEDLMSG = NumToCut || " lines were cut from the ",
                               "current file"
   end
   else do
      ZEDSMSG = NumToCut || " lines plus " || PrevMaxSave || ,
                               " lines cut"
      ZEDLMSG = "A total of " || PrevMax || " lines have been cut"
   end
   "SETMSG MSG(ISRZ000)"
 end

 exit

